grammar Pugs::Grammar::MiniPerl6;

token EscapeChar {
    \\ | \"
}

token String {
    \" [ <-[\\"]> | \\ <?EscapeChar> ]* \"
}

token VariableName {
    [<alpha> | _] [<alpha> | _ | \d]*
}

token CaptureVariableName {
    \< <VariableName> \>  { use v5; return $<VariableName> }
    | <VariableName>      { use v5; return $<VariableName> }
    | $<cap_num> := (\d+) { use v5; return "capture_" . $<cap_num> }
}

token Variable {
    \$ <CaptureVariableName>
    { use v5; return $<CaptureVariableName> }
}

token Constants {
    <String>
    | \d+
}

rule ArrayTail {\]|,<?ws>?<Expr><?ws>?<ArrayTail>{
      use v5;
      return ', ' . $<Expr> . $<ArrayTail>;
    }
}

rule ArrayRef {\[<?ws>?<Expr><?ws>?<ArrayTail>{
      use v5;
      return '[' . $<Expr> . $<ArrayTail>;
    }
}

token FunctionName { <VariableName> }

rule Parameters {<Expr>[<?ws>?,<?ws>?<Parameters>]?{ use v5;
      if($<Parameters>[0]){
        return $<Expr> . " " . $<Parameters>[0];
      }else{
        return $<Expr>;
      }
    }
}

rule FunctionAppExpr {<Variable>{ use v5; return $<Variable>
    }|<Constants>{ use v5; return $<Constants>
    }|<ArrayRef>{ use v5; return $<ArrayRef>
    }|<FunctionName>[<?ws>?<'('><?ws>?<Parameters><')'>]?{
      use v5;
      if($<Parameters>[0]){
	return "(" . $<FunctionName> . " " . $<Parameters>[0] . ")";
      }else{
	return $<FunctionName>;
      }
}}

rule AdditiveExpr {<FunctionAppExpr><?ws>?[$<op>:=(\+|\-|~)<?ws>?<AdditiveExpr>]?{
      use v5;
      if($<op>[0]){
	  my $op = $<op>[0] eq '~' ? '++' : $<op>[0];
	  return "(" . $<FunctionAppExpr> . " " . $op . " " . $<AdditiveExpr>[0] . ")";
      }else{
	  return $<FunctionAppExpr>;
      }
}}

rule Expr {<AdditiveExpr>{ use v5; return $<AdditiveExpr> }}

rule Return {<'return'> <Expr>\;?{ use v5; return "return \$ " . $<Expr> }}

rule VariableDeclaration {<'my'> <Variable><?ws>?<'='><?ws>?<Expr>\;{
	use v5; return "let " . $<Variable> . " = " . $<Expr> . " in\n"
}}

rule ProductionRule :p {<?ws>?<VariableDeclaration><?ws>?<ProductionRule>{
	use v5; return $<VariableDeclaration> . $<ProductionRule>
    }|<?ws>?<Return>{ use v5; return $<Return>
}}
