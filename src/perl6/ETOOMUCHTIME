#!/usr/local/bin/perl

use strict;
use warnings;
use English;
use Benchmark;
use File::Path;
use File::Basename;
use File::Copy;
use POSIX qw(nice); 

#let us play nice with others
nice(19);

print <<WARNING;

My job is to run STD_syntax_highlight over all the tests in t/spec.
And since STD.pm is slow at the moment, this is going to take 
too much time.

You have been warned ;-)
You can press CTRL-C when you feel bored.

WARNING

my $dir_to_test = shift @ARGV || '../../t/spec/';
my $fail = 0;
my $success = 0;
my @files = sort `find $dir_to_test -name '*.t'`;
chomp(@files);
$OUTPUT_AUTOFLUSH = 1;

my $total = 0+@files;
print "Going to run $total tests... Maybe watch a movie meanwhile?\n";

my $HILITE = "STD_syntax_highlight";
my $JQUERY_JS = "jquery-1.2.6.pack.js";
my $start_time = new Benchmark;
my @failed;
for my $file (@files) {
    my $myfile = $file;
    $myfile =~ s/^\.\.\/\.\.\/t\/spec/html/;   
    my ($simple_html,$full_html) = ("$myfile.simple.html", "$myfile.html");
    my ($html_filename,$html_path,$html_suffix) = fileparse($full_html);
    mkpath $html_path;
   
    #run the process and time it
    print <<"OUT";

$file
    -> $full_html
    -> $simple_html
OUT

    my $t0 = new Benchmark;
    system "./$HILITE --clean-html --simple-html=$simple_html --full-html=$full_html $file";
    print "It took " . 
        timestr(timediff(new Benchmark,$t0),"nop") . "\n";

    if ($CHILD_ERROR) {
        push(@failed,$file);
        $fail++;
        # catch those pesky CTRL-Cs
        if($CHILD_ERROR &= 127) {
            print "CTRL-C detected... bye bye\n";
            last;
        }
    } else {
	    $success++;
	    print "ok\n";
    }
}

printf "\nPassed $success/$total, %6.2f%%\n", $success/$total * 100;
print "It took " .
    timestr(timediff(new Benchmark,$start_time),"noc") . "\n";

if (@failed) {
    print "Failed tests:\n";
    for my $file (@failed) {
    	print "$file\n";
    }
}

#Copy javascript/css files and inform the user
print "\nCopying javascript and css files to html directory..\n";
copy("$HILITE.js","html/$HILITE.js") or die "Could not copy: $OS_ERROR\n";
copy("$JQUERY_JS", "html/$JQUERY_JS") or die "Could not copy: $OS_ERROR\n";
copy("$HILITE.css", "html/$HILITE.css") or die "Could not copy: $OS_ERROR\n";
print "\nThe output is now in the html directory. Thanks for your time ;-)\n";

