#!/usr/local/bin/perl
use lib '../../src/perl6';
use lib 'src';
use STD;
package Mildew;
do 'viv';
use AST;
use AST::Helpers;
use DumpMatch;
use utf8;
use YAML::XS;
use Encode;
use strict;
use warnings;
use Getopt::Long;
use Carp 'confess';
use Scalar::Util 'blessed';

my ($desugar,$debug,$file,$exec);
GetOptions("exec"=>\$exec,"file=s"=>\$file,"debug"=>\$debug,"desugar"=>\$desugar);

sub XXX {
    confess join ' ', 'unimplemented: ', @_;
}
sub FETCH {
    my $arg = shift;
    call FETCH => $arg
}
sub lookup {
    my $thing = shift;
    call lookup => reg '$scope',[string $thing];
}
sub capturize {
    my ($pos,$named) = @_;
    AST::Call->new(
        identifier => string "capturize",
        capture => AST::Capture->new(
            invocant => reg 'Â¢SMOP__S1P__Capturize',
            positional => $pos // [],
            named => $named // []
        )
    )
}
{ package VAST::Base;
    sub as_constant_string {
        my $m = shift;
        use YAML::XS;
        die Dump($m)." can't be represented as a constant string\n";
    }
}

sub VAST::ws::emit_m0ld {}

sub VAST::longname::canonical {
    my $m = shift;
    my $name = $m->{name}{identifier}{TEXT};
    my $v = $m->{colonpair}[0]{v}{nibble}{nibbles}[0];
    if ($v) {
        $name . ':' . $v;
    } else {
        $name;
    }
}
sub VAST::integer::emit_m0ld {
    my $m = shift;
    #XXX non-base 10
    integer($m->{TEXT});
}
sub VAST::scope_declarator::emit_m0ld {
    my $m = shift;
    if ($m->{'sym'} eq 'my') {
        if (my $decl = $m->{scoped}{declarator}) {
            if (my $var_decl = $decl->{variable_declarator}) {
                AST::Call->new(
                    identifier => string 'BIND',
                    capture => AST::Capture->new(
                        invocant => AST::Call->new(
                            identifier => string 'postcircumfix:{ }',
                            capture => AST::Capture->new(invocant=>reg '$scope',positional=>[string varname($var_decl->{variable})])
                        ),
                        positional => [
                            AST::Call->new(
                                identifier => string 'new',
                                capture => AST::Capture->new(invocant => FETCH(lookup 'Scalar')),
                            )
                        ]
                    ),
                );
            } elsif (my $routine_decl = $decl->{routine_declarator}) {
                AST::Call->new(
                    identifier => string 'BIND',
                    capture => AST::Capture->new(
                        invocant => AST::Call->new(
                            identifier => string 'postcircumfix:{ }',
                            capture => AST::Capture->new(
                                 invocant => reg '$scope',
                                 positional=>[string '&'.$routine_decl->{routine_def}{deflongname}[0]{name}{identifier}{TEXT}])
                        ),
                        positional => [
                            $routine_decl->{routine_def}->emit_m0ld()
                        ]
                    ),
                );
            } else {
                XXX('unknown scope declarator');
            }
        } else {
            XXX('scoped declarator without a declarator');
        }
    } else {
        XXX('unknown sym in scope declarator');
    }
}

sub VAST::noun::emit_m0ld {
    my $m = shift;
    if ($m->{variable}) {
        $m->{variable}->emit_m0ld;
    } elsif ($m->{value}) {
        $m->{value}->emit_m0ld;
    } elsif ($m->{routine_declarator}) {
        if ($m->{routine_declarator}{routine_def}) {
            $m->{routine_declarator}{routine_def}->emit_m0ld;
        } elsif ($m->{routine_declarator}{method_def}) {
            $m->{routine_declarator}{method_def}->emit_m0ld;
        } else {
            XXX;
        }
    } elsif ($m->{term}) {
        $m->{term}->emit_m0ld;
    } elsif ($m->{scope_declarator}) {
        $m->{scope_declarator}->emit_m0ld;
    } elsif ($m->{package_declarator}) {
        my $p = $m->{package_declarator};
        AST::Package->new
            ( sym => $p->{sym},
              name => $p->{package_def}{module_name}[0]{longname}{name}{identifier}{TEXT},
              block => $p->{package_def}{block}->emit_m0ld );
    } elsif ($m->{circumfix}) {
        $m->{circumfix}->emit_m0ld;
    } else {
        XXX;
    }
}

sub VAST::term::emit_m0ld {
    my $m = shift;
    if ($m->{identifier} && $m->{args}) {
        my $func = lookup('&'.$m->{identifier}{TEXT});
        call 'postcircumfix:( )' => FETCH($func),[capturize($m->{args}->emit_m0ld)];
    } elsif (my $name = $m->{longname}->{name}) {
        if ($name->{morename} and !$name->{identifier}) {
            lookup join '',map {$_->{identifier}[0]{TEXT}} @{$name->{morename}};
        }
    }
}
sub VAST::identifier::emit_m0ld {
    my $m = shift;
}
sub VAST::Terminator::emit_m0ld {
    my $m = shift;
    if ($m->{noun}) {
        $m->{noun}->emit_m0ld;
    } else {
        XXX;
    }
}

sub VAST::args::emit_m0ld {
    my $m = shift;
    if ($m->{listopargs}) {
        my $positional = $m->{semilist}{statement}[0];
        [(map {$_->{EXPR}->emit_m0ld} @{$m->{listopargs}}),($positional ? $positional->emit_m0ld : ())];
    } else {
        XXX;
    }
}

sub VAST::routine_def::emit_m0ld {
    my $m = shift;
    AST::Call->new(identifier=>string 'new',
        capture => AST::Capture->new(
            invocant => FETCH(lookup('Code')),
            positional => [],
            named => [
                string 'mold' => $m->{block}->emit_m0ld,
                string 'outer' => reg '$scope',
                ( $m->{multisig}[0]{signature}[0] ?
                      ( string 'signature' => $m->{multisig}[0]{signature}[0]->emit_m0ld_ahsig ) : () )
            ]
        ),
    );
}


sub VAST::method_def::emit_m0ld {
    my $m = shift;
    AST::Let->new(value => FETCH(lookup('$?CLASS')), block => sub {
        my $CLASS = shift;
        call add_method => FETCH(call '^!how' => $CLASS),[$CLASS,string $m->{longname}{name}{identifier}{TEXT},
              (call new => FETCH(lookup('Code')),[],[
                   string 'mold' => $m->{block}->emit_m0ld,
                   string 'outer' => reg '$scope',
                    ( $m->{multisig}[0]{signature}[0] ?
                      ( string 'signature' => $m->{multisig}[0]{signature}[0]->emit_m0ld_ahsig ) : () )])];
    });
}

sub VAST::signature::emit_m0ld_ahsig {
    my $m = shift;

    my $pc = 0;
    my $other = 0;
    my @stmts;
    for my $param (@{$m->{parameter}}) {
        if ($m->{param_sep}[$pc]{TEXT} && $m->{param_sep}[$pc]{TEXT} =~ /\s*:\s*/) {
            push @stmts, $param->emit_m0ld_ahsig_BIND_invocant();
        } else {
            push @stmts, $param->emit_m0ld_ahsig_BIND($other);
            $other++;
        }
        $pc++;
    }

    AST::Call->new
        ( identifier => string 'new',
          capture => AST::Capture->new
          ( invocant => FETCH(lookup('AdhocSignature')),
            positional => [],
            named =>
            [ string 'BIND' => AST::Block->new
              ( regs => [qw(interpreter scope capture)],
                stmts => trailing_return(\@stmts))]));
}

sub VAST::parameter::emit_m0ld_ahsig_BIND_invocant {
    my ($m) = @_;
    if ($m->{param_var}) {
#        call STORE => (call 'postcircumfix:{ }' => reg '$scope',[string $m->{param_var}{sigil}{sym}.$m->{param_var}{identifier}[0]{TEXT} ]),[call FETCH => (call invocant => reg '$capture')];
        call BIND => (call 'postcircumfix:{ }' => reg '$scope',[string $m->{param_var}{sigil}{sym}.$m->{param_var}{identifier}[0]{TEXT} ]),[call invocant => reg '$capture'];
    } else {
        XXX;
    }
}

sub VAST::parameter::emit_m0ld_ahsig_BIND {
    my ($m, $count) = @_;
    if ($m->{param_var}) {
#        call 'STORE' => (call 'postcircumfix:{ }' => reg '$scope',[
#            string $m->{param_var}{sigil}{sym}.$m->{param_var}{identifier}[0]{TEXT}
#        ]),[(call 'FETCH' => (call 'positional' => reg '$capture',[integer($count) ]))]
        call 'BIND' => (call 'postcircumfix:{ }' => reg '$scope',[
            string $m->{param_var}{sigil}{sym}.$m->{param_var}{identifier}[0]{TEXT}
        ]),[call 'positional' => reg '$capture',[integer($count) ]];
    } else {
        XXX;
    }
}

sub trailing_return {
    my ($stmts,) = @_;
    [@{$stmts},call(goto => reg '$interpreter',[call back=>call(continuation => reg '$interpreter')])];
}
sub VAST::block::emit_m0ld {
    my $m = shift;
    AST::Block->new(regs=>['interpreter','scope'],stmts=>trailing_return($m->{statementlist}->emit_m0ld));
}
sub VAST::circumfix::emit_m0ld {
    my $m = shift;
    if ($m->{sym}[0] eq '{' && $m->{sym}[1] eq '}' && scalar @{$m->{sym}} == 2) {
        call new=>FETCH(lookup('Code')),[],[string 'mold' => $m->{pblock}{block}->emit_m0ld,string 'outer' => reg '$scope'];
    } else {
        XXX;
    }
}
sub VAST::value::emit_m0ld {
    my $m = shift;
    if ($m->{number}) {
        $m->{number}->emit_m0ld;
    } elsif ($m->{quote}) {
        $m->{quote}->emit_m0ld;
    } else {
        warn Dump($m);
        XXX;
    }
}
sub VAST::quote::emit_m0ld {
    my $m = shift;
    # XXX escapes
    string join '',map {ref $_ ? $_->as_constant_string : $_} @{$m->{nibble}{nibbles}};
}
sub VAST::escape::as_constant_string {
    my $m = shift;
    $m->{item}->as_constant_string;
}
sub VAST::backslash::as_constant_string {
    my $m = shift;
    if ($m->{TEXT} =~ /^[n]$/) {
        '\\' . $m->{TEXT};
    } elsif ($m->{TEXT} =~ /^\W$/) {
        $m->{TEXT};
    } else {
        XXX;
    }
}
sub VAST::number::emit_m0ld {
    my $m = shift;
    if ($m->{integer}) {
        $m->{integer}->emit_m0ld;
    } else {
        XXX;
    }
}
sub VAST::sublongname::canonical {
    my $m = shift;
}
sub varname {
    my $m = shift;
    if ($m->{desigilname}) {
        $m->{sigil}{TEXT}.$m->{desigilname}{longname}->canonical;
    } elsif ($m->{sublongname}) {
        $m->{sigil}{TEXT}.$m->{sublongname}{subshortname}{desigilname}{longname}->canonical;
    } else {
        XXX;
    }
}
sub VAST::variable::emit_m0ld {
    my $m = shift;
    AST::Call->new(
        identifier=>string 'lookup',
        capture=>AST::Capture->new(invocant=>reg '$scope',positional=>[string varname($m)]),
    );
}

#returns a list of nodes
sub VAST::Comma::emit_m0ld {
    my $m = shift;
    map {$_->emit_m0ld} @{$m->{list}};
}
sub VAST::nulltermish::emit_m0ld {
    my $m = shift;
    $m->{termish}[0] ? $m->{termish}[0]->emit_m0ld : ();
}

sub VAST::dotty::emit_m0ld {
    my ($m,$noun) = @_;
    if (my $methodop = $m->{dottyop}{methodop}) {
        if ($methodop->{longname}) {
            my $positional = $methodop->{semilist}[0]{statement}[0];
            my @positional = $positional ? $positional->emit_m0ld : ();
            my $ident = $methodop->{longname}->canonical;
            if ($m->{sym} eq '.^!') {
                $ident = '^!' . $ident;
            }
            AST::Call->new(
                identifier=>string $ident,
                capture=>AST::Capture->new(invocant=>FETCH($noun),positional=>[@positional]),
            );
        } else {
            XXX('unknown methodop');
        }
    } elsif (my $postop = $m->{dottyop}{postop}) {
        if (my $postcircumfix = $postop->{postcircumfix}) {
            my $positional = $postcircumfix->{semilist}{statement}[0];
            my @positional = $positional ? $positional->emit_m0ld : ();
            if ($postcircumfix->{sym}[0] eq '(' && $postcircumfix->{sym}[1] eq ')') {
                call 'postcircumfix:( )' => FETCH($noun),[capturize([@positional])];
            } else {
                call ('postcircumfix:'.$postcircumfix->{sym}[0].' '.$postcircumfix->{sym}[1] => FETCH($noun),[@positional]);
            }
        } else {
            XXX('unknown postop');
        }
    } else {
        XXX('unknown dotty');
    }
}
sub EXPR {
    my $m = shift;
    if ($m->{noun}) {
        my $noun = $m->{noun}->emit_m0ld;
        if ($m->{POST}) {
            for (@{$m->{POST}}) {
                if ($_->{dotty}) {
                    $noun = $_->{dotty}->emit_m0ld($noun);
                } else {
                    XXX
                }
            }
            $noun;
        } else {
            $noun;
        }
    } else {
        XXX;
    }
} 
sub VAST::EXPR::emit_m0ld {
    EXPR(shift);
}
sub VAST::termish::emit_m0ld {
    EXPR(shift);
}
sub VAST::Methodcall::emit_m0ld {
    my $m = shift;
    $m->{arg}->emit_m0ld;
}
sub VAST::List_assignment::emit_m0ld {
    my $m = shift;
    if ($m->{infix}{TEXT} eq '=') {
        AST::Call->new(
            identifier=>string 'STORE',
            capture=>AST::Capture->new(invocant=>$m->{left}->emit_m0ld,positional=>[FETCH($m->{right}->emit_m0ld)]),
        );
    } else {
        XXX;
    }
}
sub VAST::Item_assignment::emit_m0ld {
    my $m = shift;
    if ($m->{infix}{TEXT} eq ':=') {
        AST::Call->new(
            identifier=>string 'BIND',
            capture=>AST::Capture->new(invocant=>$m->{left}->emit_m0ld,positional=>[$m->{right}->emit_m0ld]),
        );
    } else {
        XXX;
    }
}
sub VAST::statement::emit_m0ld {
    my $m = shift;
    if ($m->{label}) {
        AST::Label->new(label=>label($m->{label}),stmt=>statement($m->{statement}));
    } elsif ($m->{statement_control}) {
        my $stm = $m->{statement_control};

        if ($stm->{sym} eq 'if') {
            my $then = AST::Call->new(identifier=>string 'postcircumfix:( )',capture => AST::Capture->new(
                invocant => AST::Call->new(identifier=>string 'new',
                    capture => AST::Capture->new(
                        invocant => FETCH(lookup('Code')),
                        positional => [],
                        named => [
                            string 'mold' => $stm->{xblock}{pblock}{block}->emit_m0ld,
                            string 'outer' => reg '$scope'
                    ])
                ),
                positional => [capturize]
            ));

            my $else;
            if (ref $stm->{else} eq 'ARRAY' &&
                blessed $stm->{else}[0] &&
                ref $stm->{else}[0]{pblock} &&
                ref $stm->{else}[0]{pblock}{block}) {

                $else = AST::Call->new
                  ( identifier=>string 'postcircumfix:( )',
                    capture => AST::Capture->new
                    ( invocant => AST::Call->new
                      ( identifier=>string 'new',
                        capture => AST::Capture->new
                        ( invocant => FETCH(lookup('Code')),
                          positional => [],
                          named =>
                          [ string 'mold' => $stm->{else}[0]{pblock}{block}->emit_m0ld,
                            string 'outer' => reg '$scope' ])),
                      positional => [capturize]));
            }

            my @elsif;
            if (ref $stm->{elsif} eq 'ARRAY') {
                foreach my $elsif_part (@{$stm->{elsif}}) {

                    my $elsif = AST::Call->new
                      ( identifier => string 'postcircumfix:( )',
                        capture => AST::Capture->new
                        ( invocant => AST::Call->new
                          ( identifier => string 'new',
                            capture => AST::Capture->new
                            ( invocant => FETCH(lookup('Code')),
                              positional => [],
                              named =>
                              [ string 'mold' => $elsif_part->{xblock}{pblock}{block}->emit_m0ld,
                                string 'outer' => reg '$scope' ])),
                          positional => [capturize]
                        ));

                    push @elsif, AST::If->new
                      ( cond => $elsif_part->{xblock}{EXPR}->emit_m0ld,
                        then => $elsif );
                }
            }

            AST::If->new
                ( cond => $stm->{xblock}{EXPR}->emit_m0ld,
                  then => $then,
                  else => $else,
                  elsif => \@elsif );

        } else {
            XXX('unkown sym in statement_control')
        }
    } elsif ($m->{EXPR}) {
        $m->{EXPR}->emit_m0ld;
    } else {
        XXX('unknown statement')
    }
}
sub VAST::comp_unit::emit_m0ld {
    my $m = shift;
    AST::Block->new(regs=>['interpreter','scope'],stmts=>$m->{statementlist}->emit_m0ld);
}
sub VAST::statementlist::emit_m0ld {
    my $m = shift;
    [map {$_->emit_m0ld} @{$m->{statement}}]
}
my $m;
if ($file) {
    $m = STD->parsefile($file,'comp_unit');
} else {
    $m = STD->parse(join('', <STDIN>), 'comp_unit');
}

#print dump_match('comp_unit'=>$m,{vertical=>1,mark_arrays=>1,visit_twice=>1}),"\n" if $debug;
print "#################\n" if $debug;
print Dump($m->item) if $debug;
print Dump($m->item->emit_m0ld) if $debug;
print "#################\n" if $debug;
my $mold = $m->item->emit_m0ld;
use YAML::XS;
print $mold->pretty,"\n" if $desugar;
exit if $desugar;
my $m0ld = <<'BOILERPLATE'.$mold->emit_('$main').<<'CALL_MAIN';
my $void;
BOILERPLATE
my $scope = ?SMOP__S1P__LexicalPrelude;
my $Code_scalar = $scope."lookup"("Code");
my $Code = $Code_scalar."FETCH"();
my $main_code = $Code."new"(:"outer"($scope),:"mold"($main));
my $capture = Â¢SMOP__S1P__Capturize."capturize"();
my $void = $main_code."postcircumfix:( )"($capture);
CALL_MAIN
if ($exec) {
    local $ENV{LD_LIBRARY_PATH} = '../smop/build';
    open(my $m0ld_exe,"|../smop/m0ld/dist/build/m0ld/m0ld --exec");
    binmode $m0ld_exe, ':utf8';
    print $m0ld_exe $m0ld;
} else {
    binmode STDOUT, ':utf8';
    print $m0ld;#encode('utf8',$m0ld);
}
